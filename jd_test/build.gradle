if (jd_test_module_library.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (!jd_test_module_library.toBoolean()) {
            applicationId "com.dating.jd_test"
        }

        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (jd_test_module_library.toBoolean()) {
                manifest.srcFile "src/main/release/AndroidManifest.xml"
                java.srcDirs += "src/main/release/java/"
            } else {
                manifest.srcFile "src/main/debug/AndroidManifest.xml"
                java.srcDirs += "src/main/debug/java/"
            }
        }
    }

//    resourcePrefix "test_"

    // 强制改变所有引用com.android.support版本
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion rootProject.ext.supportLibVersion
                }
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':jd_common')

    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.3'

}
